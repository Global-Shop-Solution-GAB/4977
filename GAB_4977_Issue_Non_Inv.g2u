Program.Sub.ScreenSU.Start
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
	V.Global.sDirVending.Declare
	V.Global.sDirComplete.Declare
	V.Global.sDirFailed.Declare
	V.Global.sDirExceptions.Declare
	V.Global.sFileVending.Declare
	V.Global.sFileVendingFile.Declare
	V.Global.sFileComplete.Declare
	V.Global.sFileFailed.Declare
	V.Global.sFileExceptions.Declare
	V.Global.sFileExceptionsFile.Declare
Program.Sub.Preflight.End

Program.Sub.Main.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	F.ODBC.Connection!Con.OpenCompanyConnection(500)
	
	F.Intrinsic.Control.CallSub(CheckDirectories)
	F.Intrinsic.Control.CallSub(GetFiles)
	
	F.ODBC.Connection!con.Close

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.CheckDirectories.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	F.Intrinsic.String.Build("{0}\Vending\",V.Caller.FilesDir,V.Global.sDirVending)
	F.Intrinsic.File.CreateDir(V.Global.sDirVending)
	F.Intrinsic.String.Build("{0}\Vending\Complete\",V.Caller.FilesDir,V.Global.sDirComplete)
	F.Intrinsic.File.CreateDir(V.Global.sDirComplete)
	F.Intrinsic.String.Build("{0}\Vending\Failed\",V.Caller.FilesDir,V.Global.sDirFailed)
	F.Intrinsic.File.CreateDir(V.Global.sDirFailed)
	F.Intrinsic.String.Build("{0}\Vending\Exceptions\",V.Caller.FilesDir,V.Global.sDirExceptions)
	F.Intrinsic.File.CreateDir(V.Global.sDirExceptions)

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.CheckDirectories.End

Program.Sub.GetFiles.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sFiles.Declare
	V.Local.iCnt.Declare

	F.Intrinsic.File.GetFileList(V.Global.sDirVending,V.Local.sFiles)
	F.Intrinsic.String.Split(V.Local.sFiles,":",V.Local.sFiles)
	F.Intrinsic.Control.If(V.Local.sFiles,=,"")
		F.Intrinsic.Control.End
	F.Intrinsic.Control.Else
		F.Intrinsic.Control.For(V.Local.iCnt,0,V.Local.sFiles.UBound,1)
			V.Global.sFileVendingFile.Set(V.Local.sFiles(V.Local.iCnt))
			F.Intrinsic.String.Build("{0}{1}",V.Global.sDirVending,V.Local.sFiles(V.Local.iCnt),V.Global.sFileVending)
			F.Intrinsic.String.Build("{0}{1}_{2}_{3}",V.Global.sDirComplete,V.Ambient.Date.FormatMMDDYYYY,V.Ambient.Time.FormatHHNNSS,V.Local.sFiles(V.Local.iCnt),V.Global.sFileComplete)
			F.Intrinsic.String.Build("{0}{1}_{2}_{3}",V.Global.sDirFailed,V.Ambient.Date.FormatMMDDYYYY,V.Ambient.Time.FormatHHNNSS,V.Local.sFiles(V.Local.iCnt),V.Global.sFileFailed)
			F.Intrinsic.String.Build("{0}_{1}_Exception.csv",V.Ambient.Date.FormatMMDDYYYY,V.Ambient.Time.FormatHHNNSS,V.Global.sFileExceptionsFile)
			F.Intrinsic.String.Build("{0}{1}",V.Global.sDirExceptions,V.Global.sFileExceptionsFile,V.Global.sFileExceptions)
			F.Intrinsic.Control.CallSub(ImportData)
		F.Intrinsic.Control.Next(V.Local.iCnt)
	F.Intrinsic.Control.EndIf

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.GetFiles.End

Program.Sub.ImportData.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sLines.Declare
	V.Local.iCnt.Declare
	V.Local.sData.Declare
	V.Local.sUpload.Declare
	V.Local.sPart.Declare
	V.Local.sDescription.Declare
	V.Local.dDate.Declare
	V.Local.sTime.Declare
	V.Local.sEmp.Declare
	V.Local.sJob.Declare
	V.Local.sSuffix.Declare
	V.Local.sSeq.Declare
	V.Local.sQuantity.Declare
	V.Local.sCost.Declare
	V.Local.sFile.Declare
	V.Local.sSQL.Declare
	V.Local.sRet.Declare
	V.Local.sExcept.Declare
	V.Local.sEmail.Declare
	V.Local.sAttachments.Declare
	V.Local.sSender.Declare
	V.Local.sRecipient.Declare
	
'0 Part
'1 Description
'2 Date
'3 Time
'4 Employee
'5 Job
'6 Suffix
'7 Seq
'8 Quantity
'9 Cost
	
	F.Intrinsic.File.File2String(V.Global.sFileVending,V.Local.sLines)
	F.Intrinsic.String.Split(V.Local.sLines,V.Ambient.NewLine,V.Local.sLines)
	F.Intrinsic.Control.For(V.Local.iCnt,0,V.Local.sLines.UBound,1)
		F.Intrinsic.Control.If(V.Local.sLines(v.Local.iCnt),<>,"")
			F.Intrinsic.String.Split(V.Local.sLines(v.Local.iCnt),",",V.Local.sData)
			'Part
			V.Local.sPart.Set(V.Local.sData(0))
			F.Intrinsic.String.Replace(V.Local.sPart,"""","",V.Local.sPart)
			'Description
			V.Local.sDescription.Set(V.Local.sData(1))
			F.Intrinsic.String.Replace(V.Local.sDescription,"""","",V.Local.sDescription)
			F.Intrinsic.String.Left(V.Local.sDescription.Trim,30,V.Local.sDescription)
			'Date
			V.Local.dDate.Set(V.Local.sData(2))
			'Time
			V.Local.sTime.Set(V.Local.sData(3))
			V.Local.sTime.Set("120000")
			'Employee
			V.Local.sEmp.Set(V.Local.sData(4))
			F.Intrinsic.String.Replace(V.Local.sEmp,"""","",V.Local.sEmp)
			F.Intrinsic.String.Trim(V.Local.sEmp,V.Local.sEmp)
			'Job
			V.Local.sJob.Set(V.Local.sData(5))
			'Suffix
			V.Local.sSuffix.Set(V.Local.sData(6))
			'Seq
			V.Local.sSeq.Set("995000")
			'Quantity
			V.Local.sQuantity.Set(V.Local.sData(8))
			F.Intrinsic.String.Concat(V.Local.sQuantity,"00",V.Local.sQuantity)
			'Cost
			V.Local.sCost.Set(V.Local.sData(9))
			F.Intrinsic.String.Concat(V.Local.sCost,"00",V.Local.sCost)
			
			F.Intrinsic.String.Build("{0}\{1}CWTRANS.txt",V.Caller.FilesDir,V.Caller.CompanyCode,V.Local.sFile)
			F.Intrinsic.String.Build("{1}{2}{0}{3}{0}{4}{0}{5}{0}{6}{0}{7}{0}{8}{0}{0}{9}{0}{10}{0}{11}{12}", V.Ambient.Tab,V.Local.sUpload,V.Local.sPart.Trim,V.Local.sDescription.Trim,V.Local.dDate.FormatMMDDYYYY,V.Local.sTime,V.Local.sEmp.Trim,V.Local.sJob.Trim,V.Local.sSuffix.Trim,V.Local.sQuantity,V.Local.sCost,V.Caller.User,V.Ambient.NewLine,V.Local.sUpload)
			F.Intrinsic.File.String2File(V.Local.sFile,V.Local.sUpload)
			
			F.Intrinsic.String.Build("Select Job From V_Job_Header Where Job = '{0}' And Suffix = '{1}';",V.Local.sJob,V.Local.sSuffix,V.Local.sSQL)
			F.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSQL,V.Local.sRet)
			F.Intrinsic.Control.If(V.Ambient.ExecuteAndReturnEOF)
				F.Intrinsic.String.Build("{0}{1},{2},{3},{4},{5},{6},{7},{8},{9},{10},Invalid-Job || Non-Inv || File: {11}{12}", V.Local.sExcept,V.Local.sPart,V.Local.sDescription,V.Local.dDate.FormatMMDDYYYY,V.Local.sTime,V.Local.sEmp,V.Local.sJob,V.Local.sSuffix,V.Local.sSeq,V.Local.sQuantity,V.Local.sCost,V.Global.sFileFailed,V.Ambient.NewLine,V.Local.sExcept)
				F.Intrinsic.File.String2File(V.Global.sFileExceptions,V.Local.sExcept)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Next(V.Local.iCnt)
	
	F.Intrinsic.Control.If(V.Local.sExcept,=,"")
		F.Intrinsic.String.Build("{0}{1}CWTRANS.txt{0}",V.Ambient.DblQuote,V.Caller.CompanyCode,V.Local.sFile)
		F.Intrinsic.Task.LaunchGSSSync("WIR103","-c",V.Local.sFile)
		F.Intrinsic.File.CopyFile(V.Global.sFileVending,V.Global.sFileComplete)
		F.Intrinsic.File.DeleteFile(V.Global.sFileVending)
	F.Intrinsic.Control.Else
		F.Global.Security.GetUserEmail(V.Caller.User,V.Caller.CompanyCode,V.Local.sEmail)
		F.Intrinsic.Control.If(V.Local.sEmail,<>,"")
			F.Intrinsic.String.Build("{0}*!*{1}@!@{2}*!*{3}",V.Global.sFileVendingFile,V.Global.sDirVending,V.Global.sFileExceptionsFile,V.Global.sDirExceptions,V.Local.sAttachments)
			F.Intrinsic.String.Build("{0}*!*{1}",V.Local.sEmail,V.Caller.User,V.Local.sSender)
			F.Intrinsic.String.Build("{0}*!*{1}",V.Caller.User,V.Local.sEmail,V.Local.sRecipient)
			F.Global.Messaging.QueueMessage(V.Caller.CompanyCode,0,"","Vending Machine Import Failed",V.Local.sSender,V.Local.sRecipient,"View attachments for more information.",5,"",False,"","","","","","","",V.Local.sAttachments,False)
		F.Intrinsic.Control.EndIf
		F.Intrinsic.File.CopyFile(V.Global.sFileVending,V.Global.sFileFailed)
		F.Intrinsic.File.DeleteFile(V.Global.sFileVending)
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.String.Build("{0}\{1}CWTRANS.txt",V.Caller.FilesDir,V.Caller.CompanyCode,V.Local.sFile)
	F.Intrinsic.File.DeleteFile(V.Local.sFile)

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.ImportData.End

Program.Sub.Comments.Start
${$5$}$2.0.0.0$}$2
${$6$}$avaldez$}$20230502154223584$}$CBUu04veoT9NExad5Mkb3ylWEN0INN4AG0RpZu2cQ9qKdkdeN3U5NS2oHetb0N/mind4O6oKQKA=
Program.Sub.Comments.End